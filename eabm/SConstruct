#! /usr/bin/env python

import os
import sys
import pathlib

import setuptools_scm
# Only required when WAVES is not installed as a package, e.g. for local testing in SCons-simulation repository.
try:
    import waves
except ModuleNotFoundError:
    sys.path.insert(0, str(pathlib.Path('..').resolve()))
    import waves

# TODO: make this available for overwrite from a command line option
# https://re-git.lanl.gov/kbrindley/scons-simulation/-/issues/25
variant_dir_base = pathlib.Path('build')

# Set project internal variables
project_name = 'SCons-EABM'
abaqus_source_dir = pathlib.Path('abaqus')
documentation_source_dir = 'docs'

# Inherit user's full environment and set project variables
env = Environment(ENV=os.environ.copy(),
                  PROJECT_NAME=project_name.lower(),
                  # TODO: Figure out how to use the SCons-simulation version from a subdirectory
                  #VERSION=setuptools_scm.get_version(),
                  VERSION='uknown',
                  PROJECT_DIR=Dir('.').abspath,
                  ABAQUS_SOURCE_DIR=str(abaqus_source_dir))

# Add custom builders
env.Append(BUILDERS={'AbaqusJournal': waves.builders.abaqus_journal(),
                     'AbaqusSolver': waves.builders.abaqus_solver()})

# Add top-level SCons script
SConscript(dirs='.', variant_dir=str(variant_dir_base), exports='documentation_source_dir', duplicate=False)

# Add documentation target
build_dir = variant_dir_base / documentation_source_dir
SConscript(dirs=documentation_source_dir, variant_dir=str(build_dir), exports='env')

# Add simulation targets
eabm_simulation_directories = [
    'tutorial_01_geometry',
    'tutorial_02_partition_mesh',
    'tutorial_03_solverprep',
    'tutorial_04_simulation'
]
for source_dir in eabm_simulation_directories:
    build_dir = variant_dir_base / source_dir
    SConscript(dirs=source_dir, variant_dir=build_dir, exports='env', duplicate=False)
