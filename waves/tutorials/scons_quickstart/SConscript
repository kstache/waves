Import('env')

# Write project builders for re-use in task definitions
abaqus_journal = Builder(
    action="cd ${TARGET.dir.abspath} && ${abaqus_program} cae -noGui ${SOURCE.abspath} " \
           "${abaqus_options} -- ${journal_options}")

abaqus_solver = Builder(
    action="cd ${TARGET.dir.abspath} && ${abaqus_program} -job ${job_name} -input ${SOURCE.filebase} " \
           "${abaqus_options} -interactive -ask_delete no")

# Add custom builders
env.Append(BUILDERS={
    'AbaqusJournal': abaqus_journal,
    'AbaqusSolver': abaqus_solver
})

# Geometry
env.AbaqusJournal(
    target="rectangle_geometry.cae",
    source="rectangle_geometry.py",
    abaqus_program=env['abaqus']
)

# Partition
env.AbaqusJournal(
    target="rectangle_partition.cae",
    source=["rectangle_partition.py", "rectangle_geometry.cae"],
    abaqus_program=env['abaqus']
)

# Mesh
env.AbaqusJournal(
    target=["rectangle_mesh.inp", "rectangle_mesh.cae"],
    source=["rectangle_mesh.py", "rectangle_partition.cae", "abaqus_journal_utilities.py"],
    abaqus_program=env['abaqus']
)

# SolverPrep
abaqus_source_list = [
    "rectangle_compression.inp",
    "assembly.inp",
    "boundary.inp",
    "field_output.inp",
    "materials.inp",
    "parts.inp",
    "rectangle_mesh.inp",
    "history_output.inp"
]

solve_targets = [
    "rectangle_compression.odb",
    "rectangle_compression.dat",
    "rectangle_compression.msg",
    "rectangle_compression.com",
    "rectangle_compression.prt",
    "rectangle_compression.sta"
]

target = env.AbaqusSolver(
    target=solve_targets,
    source=abaqus_source_list,
    abaqus_program=env['abaqus'],
    job_name="rectangle_compression",
    abaqus_options="-double both"
)

# Part Image
image = env.AbaqusJournal(
    target=["rectangle_image.png"],
    source=["export_abaqus_image.py"] + abaqus_source_list,
    abaqus_program=env['abaqus'],
    journal_options="--input-file rectangle_compression.inp --output-file ${TARGET.file}"
)

# Collector alias named after the model simulation
env.Alias("rectangle", target)
env.Alias("image", image)
