#! /usr/bin/env python

import os
import sys
import pathlib
import subprocess

import waves

# Comments used in tutorial code snippets: marker-1

# Accept command line options with fall back default values
AddOption(
    "--build-dir",
    dest="variant_dir_base",
    default="build",
    nargs=1,
    type="string",
    action="store",
    metavar="DIR",
    help="SCons build (variant) root directory. Relative or absolute path. (default: '%default')"
)
AddOption(
    "--unconditional-build",
    dest="unconditional_build",
    default=False,
    action="store_true",
    help="Boolean flag to force building of conditionally ignored targets, e.g. if the target's action program is missing" \
            " and it would normally be ignored. (default: '%default')"
)
AddOption(
    "--print-build-failures",
    dest="print_build_failures",
    default=False,
    action="store_true",
    help="Print task *.stdout target file(s) on build failures. (default: '%default')"
)

# Comments used in tutorial code snippets: marker-2

# Inherit user's full environment and set project variables
env = waves.scons_extensions.WAVESEnvironment(
    ENV=os.environ.copy(),
    variant_dir_base=GetOption("variant_dir_base"),
    unconditional_build=GetOption("unconditional_build"),
    print_build_failures=GetOption("print_build_failures")
)

# Conditionally print failed task *.stdout files
env.PrintBuildFailures(print_stdout=env["print_build_failures"])

# Comments used in tutorial code snippets: marker-3

# Find required programs for conditional target ignoring and absolute path for use in target actions
env['abaqus'] = env.AddProgram(["/apps/abaqus/Commands/abq2023", "abq2023"])
env["fierro"] = env.AddProgram(["fierro-parallel-implicit"])

# Cubit Python requires a separate construction environment to avoid putting Cubit distributed Python packages in the
# parent Python 3 environment's PYTHONPATH
envCubit = Environment(ENV=os.environ.copy())
envCubit["python"] = waves.scons_extensions.add_cubit_python(envCubit, ["/apps/Cubit-16.12/cubit", "cubit"])

# Sierra requires a separate construction environment
try:
    envSierra = waves.scons_extensions.shell_environment("module use /projects/aea_compute/modulefiles && module load sierra")
except subprocess.CalledProcessError:
    print("Failed to initialize the Sierra environment", file=sys.stderr)
    envSierra = Environment()
envSierra["sierra"] = waves.scons_extensions.add_program(envSierra, ["sierra"])

# Comments used in tutorial code snippets: marker-4

# Set project internal variables and variable substitution dictionaries
project_name = 'WAVES-TUTORIAL'
version = '0.1.0'
project_dir = pathlib.Path(Dir(".").abspath)
abaqus_source_dir = "modsim_package/abaqus"
cubit_source_dir = "modsim_package/cubit"
sierra_source_dir = "modsim_package/sierra"
fierro_source_dir = "modsim_package/fierro"
project_variables = {
    'project_name': project_name,
    'project_dir': project_dir,
    'version': version,
    'abaqus_source_abspath': project_dir / abaqus_source_dir,
    'cubit_source_abspath': project_dir / cubit_source_dir,
    'sierra_source_abspath': project_dir / sierra_source_dir,
    'fierro_source_abspath': project_dir / fierro_source_dir
}
for key, value in project_variables.items():
    env[key] = value

# Comments used in tutorial code snippets: marker-5

# Build path object for extension and re-use
variant_dir_base = pathlib.Path(env['variant_dir_base'])

# Add builders and pseudo-builders
env.Append(BUILDERS={
    'AbaqusJournal': waves.scons_extensions.abaqus_journal(program=env['abaqus']),
    'AbaqusSolver': waves.scons_extensions.abaqus_solver(program=env['abaqus']),
    'PythonScript': waves.scons_extensions.python_builder_factory(),
    'FierroImplicit': waves.scons_extensions.fierro_implicit_builder_factory()
})

envCubit.Append(BUILDERS={
    "PythonScript": waves.scons_extensions.python_builder_factory(program=envCubit["python"])
})

envSierra.Append(BUILDERS={
    'Sierra': waves.scons_extensions.sierra_builder_factory(program=envSierra['sierra'])
})

# Comments used in tutorial code snippets: marker-6

# Add simulation targets
workflow_configurations = [
    'abaqus',
    'sierra',
    "fierro"
]
for workflow in workflow_configurations:
    build_dir = variant_dir_base / workflow
    SConscript(
        workflow,
        variant_dir=build_dir,
        exports={"env": env, "envCubit": envCubit, "envSierra": envSierra},
        duplicate=False
    )

# Comments used in tutorial code snippets: marker-7

# Add default target list to help message
env.Default()  # Empty defaults list to avoid building all simulation targets by default
# Add aliases to help message so users know what build target options are available
# This must come *after* all expected Alias definitions and SConscript files.
env.ProjectHelp()

# Comments used in tutorial code snippets: marker-8
