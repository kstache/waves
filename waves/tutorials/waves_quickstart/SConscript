Import("env", "alias")

# Geometry
env.AbaqusJournal(
    target="rectangle_geometry.cae",
    source="rectangle_geometry.py"
)

# Partition
env.AbaqusJournal(
    target="rectangle_partition.cae",
    source=["rectangle_partition.py", "rectangle_geometry.cae"]
)

# Mesh
env.AbaqusJournal(
    target=["rectangle_mesh.inp", "rectangle_mesh.cae"],
    source=["rectangle_mesh.py", "rectangle_partition.cae", "abaqus_utilities.py"]
)

# Abaqus Solve
env.AbaqusSolver(
    target="rectangle_compression.odb",
    source="rectangle_compression.inp",
    job_name="rectangle_compression",
    abaqus_options="-double both"
)

# Abaqus Extract
env.AbaqusExtract(
    target=["rectangle_compression.h5"],
    source=["rectangle_compression.odb"]
)

# Post-processing
script_options = "--input-file ${SOURCES[1:].abspath} --output-file ${TARGET.file} --x-units mm/mm --y-units MPa"
target = env.PythonScript(
    target=["stress_strain.pdf", "stress_strain.csv"],
    source=["post_processing.py", "rectangle_compression_datasets.h5"],
    subcommand_options=script_options
)

# Collector alias named after the model simulation
env.Alias(alias, target)

if not env["unconditional_build"] and not env["ABAQUS_PROGRAM"]:
    print(f"Program 'abaqus' was not found in construction environment. Ignoring 'rectangle' target(s)")
    Ignore([".", alias], target)
