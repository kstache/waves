#! /usr/bin/env python

import pathlib

# Inherit the parent construction environment
Import('env')

# Perform variable substitution on Sphinx configuration file
env.Substfile(
    "conf.py.in",
    SUBST_DICT={'@PROJECT_NAME@': env['PROJECT_NAME'],
                '@VERSION@': env['VERSION'],
                '@PROJECT_DIR@': env['PROJECT_DIR'],
                '@ABAQUS_SOURCE_DIR@': env['ABAQUS_SOURCE_DIR'],
                # TODO: (1) Separate EABM and WAVES definitions
                '@abaqus_wrapper@': env['abaqus_wrapper']})

# Explicit Sphinx documentation dependency list
documentation_file_list = [
    'conf.py',
    'README.txt',  # Copied from README.rst to docs build directory by root SConscript
    'api.rst',
    'build.txt',
    'changelog.rst',
    'contribution.txt',
    'dependencies.txt',
    'devops.rst',
    'documentation.txt',
    'index.rst',
    'model_repository_api.rst',
    'model_repository_user.rst',
    'project_brief.txt',
    'references.bib',
    'release_philosophy.rst',
    'targets.txt',
    'theory.rst',
    'user.rst',
    'zreferences.rst',
    '_static/custom.css'
]

# TODO: Find the sphinx-build program with scons
# https://re-git.lanl.gov/kbrindley/scons-simulation/-/issues/3
# TODO: os agnostic pathsep for the html directory
# https://re-git.lanl.gov/kbrindley/scons-simulation/-/issues/4
sphinx_options = '-W'
sphinx_build = env.Command(
    # TODO: figure out how to keep the Command in the "build/docs" directory without a leading dummy filename
    # https://re-git.lanl.gov/kbrindley/scons-simulation/-/issues/5
    target=['dummy_file_name', 'html/index.html'],
    source=documentation_file_list,
    action="sphinx-build ${sphinx_options} -b html ${TARGET.dir.abspath} ${TARGET.dir.abspath}/html",
    sphinx_options=sphinx_options)
env.Clean(sphinx_build, 'html/')
# TODO: Figure out why alias is recognized, but not built
# https://re-git.lanl.gov/kbrindley/scons-simulation/-/issues/6
env.Alias('documentation', sphinx_build)
