#! /usr/bin/env python

import pathlib

# Inherit the parent construction environment
Import('env')

# Perform variable substitution on Sphinx configuration file
env.Substfile(
    "conf.py.in",
    SUBST_DICT={'@PROJECT_NAME@': env['PROJECT_NAME'],
                '@VERSION@': env['VERSION'],
                '@PROJECT_DIR@': env['PROJECT_DIR'],
                '@ABAQUS_SOURCE_DIR@': env['ABAQUS_SOURCE_DIR']})

# Explicit Sphinx documentation dependency list
documentation_file_list = [
    'conf.py',
    # TODO: find a more robust method to link to the README.txt target without assuming relative paths
    str(pathlib.Path(Dir('..').abspath) / 'README.txt'),
    'changelog.rst',
    'index.rst',
    'project_brief.txt',
    'references.bib',
    'release_philosophy.rst',
    'targets.txt',
    'model_repository_user.rst',
    'model_repository_api.rst',
    'build.txt',
    'documentation.txt',
    'zreferences.rst',
    '_static/custom.css'
]

# TODO: Find the sphinx-build program with scons
# https://re-git.lanl.gov/kbrindley/scons-simulation/-/issues/3
# TODO: os agnostic pathsep for the html directory
# https://re-git.lanl.gov/kbrindley/scons-simulation/-/issues/4
sphinx_options = '-W'
sphinx_build = env.Command(
    # TODO: figure out how to keep the Command in the "build/docs" directory without a leading dummy filename
    # https://re-git.lanl.gov/kbrindley/scons-simulation/-/issues/5
    target=['dummy_file_name', 'html/index.html'],
    source=documentation_file_list,
    action="sphinx-build ${sphinx_options} -b html ${TARGET.dir.abspath} ${TARGET.dir.abspath}/html",
    sphinx_options=sphinx_options)
env.Clean(sphinx_build, 'html/')
# TODO: Figure out why alias is recognized, but not built
# https://re-git.lanl.gov/kbrindley/scons-simulation/-/issues/6
env.Alias('documentation', sphinx_build)
