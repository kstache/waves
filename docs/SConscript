#! /usr/bin/env python

import pathlib

# Inherit the parent construction environment
Import(['env', 'project_substitution_dictionary'])
project_dir = pathlib.Path(env['project_dir'])

# Set empty alias return list
alias_list = []

# Perform variable substitution on Sphinx configuration file
env.Substfile(
    "conf.py.in",
    SUBST_DICT=project_substitution_dictionary)

# Explicit Sphinx documentation dependency list
documentation_file_list = [
    'conf.py',  # Generated by an SCons target
    'README.txt',  # Copied from README.rst to docs build directory by root SConscript
    'CITATION.bib',  # Copied from CITATION.bib to docs build directory by root SConscript
    'environment.txt',  # Copied from environment.txt to docs build directory by root SConscript
    'tutorials_tutorial_00_SConstruct',  # Copied from the eabm directory to docs build directory by this SConscript
    'quickstart_README.txt',   # Copied from the eabm directory to waves docs build directory by root SConscript
    'eabm_api.rst',
    'eabm_cli.rst',
    'eabm_abaqus_files.rst',
    'api.rst',
    'internal_api.rst',
    'build.txt',
    'changelog.rst',
    'citation.rst',
    'cli.rst',
    'computational_practices_introduction.rst',
    'computational_practices_version_control.rst',
    'computational_practices_documentation.rst',
    'computational_practices_build_system.rst',
    'computational_practices_compute_environment.rst',
    'computational_practices_regression_testing.rst',
    'contribution.txt',
    'dependencies.txt',
    'devops.rst',
    'documentation.txt',
    'glossary.rst',
    'index.rst',
    'man_index.rst',
    'line_number_jump_note.txt',
    'project_brief.txt',
    'references.bib',
    'release_philosophy.rst',
    'scons_quickstart.rst',
    'scons_multiactiontask.rst',
    'scons_tutorial_directory.txt',
    'scons_tutorial_environment.txt',
    'scons_tutorial_introduction.txt',
    'targets.txt',
    'tutorial_introduction.rst',
    'tutorial_quickstart.rst',
    'tutorial_core.rst',
    'tutorial_supplemental.rst',
    'tutorial_wip.rst',
    'tutorial_00_prerequisites.txt',
    'tutorial_00_scons.rst',
    'tutorial_01_geometry_waves.rst',
    'tutorial_02_partition_mesh_waves.rst',
    'tutorial_03_solverprep_waves.rst',
    'tutorial_04_simulation_waves.rst',
    'tutorial_05_parameter_substitution_waves.rst',
    'tutorial_06_include_files_waves.rst',
    'tutorial_07_cartesian_product_waves.rst',
    'tutorial_07_latin_hypercube_waves.rst',
    'tutorial_07_sobol_sequence_waves.rst',
    'tutorial_extend_study_waves.rst',
    'tutorial_08_data_extraction_waves.rst',
    'tutorial_09_post_processing_waves.rst',
    'tutorial_10_regression_testing_waves.rst',
    'tutorial_cubit.rst',
    'tutorial_mesh_convergence_waves.rst',
    'tutorial_escape_sequences_waves.rst',
    'tutorial_remote_execution_waves.rst',
    'tutorial_task_reuse_waves.rst',
    'tutorial_environment_activation.txt',
    'waves_access_and_appendix.txt',
    'waves_logo_brandmark_smaller.png',
    'waves_quickstart.rst',
    'wip_warning.txt',
    'zreferences.rst',
    '_static/custom.css'
]

# Copy tutorial files to the build/docs directory
tutorials_dir = project_dir / pathlib.Path(env['tutorials_dir'])
tutorial_files_list = [
    tutorials_dir / 'scons_quickstart_SConstruct',
    tutorials_dir / 'scons_multiactiontask_SConstruct',
    tutorials_dir / 'waves_quickstart_SConstruct',
    tutorials_dir / 'tutorial_00_SConstruct',
    tutorials_dir / 'tutorial_01_geometry_SConstruct',
    tutorials_dir / 'tutorial_02_partition_mesh_SConstruct',
    tutorials_dir / 'tutorial_03_solverprep_SConstruct',
    tutorials_dir / 'tutorial_04_simulation_SConstruct',
    tutorials_dir / 'tutorial_05_parameter_substitution_SConstruct',
    tutorials_dir / 'tutorial_06_include_files_SConstruct',
    tutorials_dir / 'tutorial_07_cartesian_product_SConstruct',
    tutorials_dir / 'tutorial_07_latin_hypercube_SConstruct',
    tutorials_dir / 'tutorial_07_sobol_sequence_SConstruct',
    tutorials_dir / 'tutorial_extend_study_SConstruct',
    tutorials_dir / 'tutorial_08_data_extraction_SConstruct',
    tutorials_dir / 'tutorial_09_post_processing_SConstruct',
    tutorials_dir / 'tutorial_10_regression_testing_SConstruct',
    tutorials_dir / 'tutorial_task_reuse_SConstruct',
    tutorials_dir / 'tutorial_cubit_SConstruct',
    tutorials_dir / 'tutorial_mesh_convergence_SConstruct',
    tutorials_dir / 'tutorial_escape_sequences_SConstruct',
    tutorials_dir / 'tutorial_remote_execution_SConstruct',
    tutorials_dir / env['tutorial_01_dir'] / 'SConscript',
    tutorials_dir / env['tutorial_02_dir'] / 'SConscript',
    tutorials_dir / env['tutorial_03_dir'] / 'SConscript',
    tutorials_dir / env['tutorial_04_dir'] / 'SConscript',
    tutorials_dir / env['tutorial_05_dir'] / 'SConscript',
    tutorials_dir / env['tutorial_06_dir'] / 'SConscript',
    tutorials_dir / env['tutorial_07_cartesian_product_dir'] / 'SConscript',
    tutorials_dir / env['tutorial_07_latin_hypercube_dir'] / 'SConscript',
    tutorials_dir / env['tutorial_07_sobol_sequence_dir'] / 'SConscript',
    tutorials_dir / env['tutorial_extend_study_dir'] / 'SConscript',
    tutorials_dir / env['tutorial_08_dir'] / 'SConscript',
    tutorials_dir / env['tutorial_09_dir'] / 'SConscript',
    tutorials_dir / env['tutorial_10_dir'] / 'SConscript',
    tutorials_dir / 'tutorial_task_reuse',
    tutorials_dir / env['tutorial_cubit_dir'] / 'SConscript',
    tutorials_dir / env['tutorial_mesh_convergence_dir'] / 'SConscript',
    tutorials_dir / env['tutorial_escape_sequences_dir'] / 'SConscript',
    tutorials_dir / env['tutorial_remote_execution_dir'] / 'SConscript',
    tutorials_dir / env['abaqus_dir'] / 'single_element_geometry.py',
    tutorials_dir / env['abaqus_dir'] / 'single_element_partition.py',
    tutorials_dir / env['abaqus_dir'] / 'single_element_mesh.py',
    tutorials_dir / env['abaqus_dir'] / 'abaqus_journal_utilities.py',
    tutorials_dir / env['abaqus_dir'] / 'single_element_compression.inp',
    tutorials_dir / env['abaqus_dir'] / 'assembly.inp',
    tutorials_dir / env['abaqus_dir'] / 'boundary.inp',
    tutorials_dir / env['abaqus_dir'] / 'field_output.inp',
    tutorials_dir / env['abaqus_dir'] / 'history_output.inp',
    tutorials_dir / env['abaqus_dir'] / 'materials.inp',
    tutorials_dir / env['abaqus_dir'] / 'parts.inp',
    tutorials_dir / env['abaqus_dir'] / 'single_element_compression.inp.in',
    tutorials_dir / env['cubit_dir'] / 'single_element_geometry.py',
    tutorials_dir / env['cubit_dir'] / 'single_element_partition.py',
    tutorials_dir / env['cubit_dir'] / 'single_element_mesh.py',
    tutorials_dir / env['python_dir'] / 'single_element_compression_nominal.py',
    tutorials_dir / env['python_dir'] / 'single_element_compression_cartesian_product.py',
    tutorials_dir / env['python_dir'] / 'single_element_compression_latin_hypercube.py',
    tutorials_dir / env['python_dir'] / 'single_element_compression_sobol_sequence.py',
    tutorials_dir / env['python_dir'] / 'single_element_compression_mesh_convergence.py',
    tutorials_dir / env['python_dir'] / 'plot_scatter.py'
]
for file_path in tutorial_files_list:
    target = f'{file_path.parts[-2]}_{file_path.name}'
    Command(target=target,
            source=str(file_path),
            action=Copy("$TARGET", "$SOURCE"))
    documentation_file_list.append(target)

# TODO: os agnostic pathsep for the html directory
# https://re-git.lanl.gov/aea/python-projects/waves/-/issues/4
sphinx_options = '-W'
html = env.Command(
    target=[Dir('html')],
    source=documentation_file_list,
    action=f"{env['sphinx_build']} ${{sphinx_options}} -b html ${{TARGET.dir.abspath}} ${{TARGET.dir.abspath}}/html",
    sphinx_options=sphinx_options)
env.Clean(html, Dir('html'))
env.AlwaysBuild(html)
alias_list.extend(env.Alias('html', html))

latexpdf = env.Command(
    target=[Dir('latex')],
    source=documentation_file_list,
    action=f"{env['sphinx_build']} -M latexpdf ${{TARGET.dir.abspath}} ${{TARGET.dir.abspath}}")
env.Clean(latexpdf, Dir('latex'))
env.AlwaysBuild(latexpdf)
alias_list.extend(env.Alias('latexpdf', latexpdf))

man = env.Command(
    target=[Dir('man')],
    source=documentation_file_list,
    action=f"{env['sphinx_build']} ${{sphinx_options}} -b man ${{TARGET.dir.abspath}} ${{TARGET.dir.abspath}}/man -t man",
    sphinx_options=sphinx_options)
env.Clean(man, Dir('man'))
env.AlwaysBuild(man)
alias_list.extend(env.Alias('man', man))

# Collector alias to build all documentation
alias_list.extend(env.Alias('documentation', html + latexpdf + man))

if not env['unconditional_build'] and not env['sphinx_build']:
    print(f"Program 'sphinx-build' was not found in construction environment. Ignoring Sphinx target(s)")
    Ignore(['.', 'html', 'html'], html)
    Ignore(['.', 'latex', 'latexpdf'], latexpdf)
    Ignore(['.', 'man', 'man'], man)

# Return the alias list to SConstruct for help message output
Return('alias_list')
